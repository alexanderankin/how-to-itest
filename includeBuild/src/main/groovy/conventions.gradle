plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
}

java {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
    withJavadocJar()
}

repositories.mavenCentral()


// https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests
sourceSets {
    itest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

// tell intellij about the sourceSet
idea {
    module {
        sourceDirs -= file('src/itest/java')
        testSources.from file('src/itest/java')
    }
}

configurations {
    // lombok
    testCompileOnly.extendsFrom(compileOnly)
    testAnnotationProcessor.extendsFrom(annotationProcessor)
    itestCompileOnly.extendsFrom(testCompileOnly)
    itestAnnotationProcessor.extendsFrom(testAnnotationProcessor)

    testImplementation.extendsFrom(implementation)
    testRuntimeOnly.extendsFrom(runtimeOnly)

    // itests
    itestImplementation.extendsFrom testImplementation
    itestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.1.3')
    annotationProcessor platform('org.springframework.boot:spring-boot-dependencies:3.1.3')
    implementation platform('org.springframework.cloud:spring-cloud-dependencies:2022.0.4')

    implementation('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'com.fasterxml.uuid:java-uuid-generator:4.3.0'
    // new in gradle 8
    // https://docs.gradle.org/current/userguide/java_testing.html
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // itestImplementation platform('org.testcontainers:testcontainers-bom:1.18.0')
    itestImplementation 'org.testcontainers:testcontainers'
}

// from same page as itest sourceSets block
def integrationTest = tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
    shouldRunAfter test
}

// pick up integrationTest coverage file in report
jacocoTestReport {
    getExecutionData().from(fileTree(project.layout.buildDirectory).include("jacoco/*.exec"))
}

check.dependsOn integrationTest

// use junit for all tests
tasks.withType(Test).configureEach { t -> t.useJUnitPlatform() }
